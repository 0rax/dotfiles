pkgname=runit-standalone
pkgver=2.1.2
pkgrel=1
pkgdesc="A service supervision scheme"
url="http://smarden.org/runit/"
license=('BSD')
arch=('i686' 'x86_64')
depends=('bash' 'util-linux')
makedepends=('coreutils' 'sed')
provides=('runit')
conflicts=('runit')
install=""
source=( http://smarden.org/runit/runit-$pkgver.tar.gz)
sha256sums=('6fd0160cb0cf1207de4e66754b6d39750cff14bb0aa66ab49490992c0c47ba18')

prepare() {
  cd "$srcdir/admin/runit-$pkgver/src"

  # do not build statically
  sed -i -e 's/ -static//g' Makefile

  # add custom CFLAGS and LDFLAGS instead of predefined ones by the source
  echo "${CC:-gcc} $CFLAGS" > conf-cc
  echo "${CC:-gcc -s} $LDFLAGS" > conf-ld
}

build() {
  cd "$srcdir/admin/runit-$pkgver/src"
  make
}

check() {
  cd "$srcdir/admin/runit-$pkgver/src"
  make check
}

package() {
  cd "$srcdir/admin/runit-$pkgver"
  install -Dm755 src/{chpst,runit,runit-init,runsv,runsvchdir,runsvdir,sv,svlogd,utmpset} -t "$pkgdir/usr/bin"
  install -Dm644 man/* -t "$pkgdir/usr/share/man/man8"
  install -Dm644 doc/*.html -t "$pkgdir/usr/share/doc/runit"
  install -Dm644 "package/COPYING" "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
